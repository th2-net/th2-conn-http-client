plugins {
	id 'com.jfrog.artifactory' version '4.15.1'
    id 'com.palantir.docker' version '0.25.0' apply false
    id 'org.jetbrains.kotlin.jvm' version "${kotlin_version}"
    id "com.github.harbby.gradle.serviceloader" version '1.1.5'
}

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'maven-publish'
apply plugin: 'com.palantir.docker'
apply plugin: 'com.jfrog.artifactory'

ext {
    sharedDir = file("${project.rootDir}/shared")
}

group = 'com.exactpro.th2'
version = release_version

sourceCompatibility = 11
targetCompatibility = 11

repositories {
    maven {
        name 'MavenLocal'
        url sharedDir
    }

    mavenCentral()

    maven {
        name 'TH2 Bintray'
        url 'https://dl.bintray.com/exactpro/th2'
    }

    maven {
        name 'Artifactory release'
        url 'http://artifactory5.exp.exactpro.com/artifactory/libs-release'
    }

    mavenLocal()

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
        resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    }
}

jar {
    manifest {
        attributes(
                'Created-By': "${System.getProperty('java.version')} (${System.getProperty('java.vendor')})",
                'Specification-Title': '',
                'Specification-Vendor': 'Exactpro Systems LLC',
                'Implementation-Title': project.archivesBaseName,
                'Implementation-Vendor': 'Exactpro Systems LLC',
                'Implementation-Vendor-Id': 'com.exactpro',
                'Implementation-Version': project.version
        )
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            name = 'localRepo'
            url = sharedDir
        }
    }
}

artifactory {
    publish {
        contextUrl = "${artifactory_url}"

        repository {
            repoKey = "${artifactory_deploy_repo_key}"
            username = "${artifactory_user}"
            password = "${artifactory_password}"
        }

        defaults {
            publications('mavenJava')
            publishArtifacts = true
            publishBuildInfo = true
            publishPom = true
        }
    }
}

dependencies {
    api platform('com.exactpro.th2:bom:2.10.1')

    implementation 'com.exactpro.th2:common:2.7.0-th2-1150-+'

    implementation 'org.slf4j:slf4j-log4j12'
    implementation 'org.slf4j:slf4j-api'

    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: kotlin_version
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: kotlin_version
    implementation group: 'io.github.microutils', name: 'kotlin-logging', version: '1.7.9'
    implementation group: 'com.athaydes.rawhttp', name: 'rawhttp-core', version: '2.4.1'
    implementation group: 'com.fasterxml.jackson.module', name: 'jackson-module-kotlin', version: '2.11.2'

    testImplementation group: 'org.jetbrains.kotlin', name: 'kotlin-test-junit5', version: kotlin_version
}

application {
    mainClassName 'com.exactpro.th2.http.client.Main'
}

applicationName = 'service'

distTar {
    archiveName "${applicationName}.tar"
}

dockerPrepare {
    dependsOn distTar
}

docker {
    copySpec.from(tarTree("$buildDir/distributions/${applicationName}.tar"))
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "11"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "11"
    }
}

serviceLoader {
    serviceInterface 'com.exactpro.th2.http.client.api.IAuthSettingsTypeProvider'
    serviceInterface 'com.exactpro.th2.http.client.api.IRequestHandler'
    serviceInterface 'com.exactpro.th2.http.client.api.IStateManager'
}